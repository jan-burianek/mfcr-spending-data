prefix obeu-attribute:   <http://data.openbudgets.eu/ontology/dsd/attribute/>
prefix obeu-currency:    <http://data.openbudgets.eu/resource/codelist/currency/>
prefix obeu-dimension:   <http://data.openbudgets.eu/ontology/dsd/dimension/>
prefix obeu-measure:     <http://data.openbudgets.eu/ontology/dsd/measure/>
prefix obeu-operation:   <http://data.openbudgets.eu/resource/codelist/operation-character/>
prefix qb:               <http://purl.org/linked-data/cube#>
prefix schema:           <http://schema.org/>
prefix xsd:              <http://www.w3.org/2001/XMLSchema#>
prefix mfcr-attribute:     <http://data.janburianek.com/ontology/dsd/mfcr-paid-invoices-2010-2014/attribute/>
prefix mfcr-observation:    <http://data.janburianek.com/resource/dataset/mfcr-paid-invoices-2010-2014/observation/>
prefix mfcr-dimension:     <http://data.janburianek.com/ontology/dsd/mfcr-paid-invoices-2010-2014/dimension/>
prefix mfcr-organization:	<http://data.janburianek.com/resource/organization/>
prefix dcterms:          <http://purl.org/dc/terms/>
prefix rov:              <http://www.w3.org/ns/regorg#>

CONSTRUCT {
    <http://data.janburianek.com/resource/dataset/mfcr-paid-invoices-2010-2014> a qb:DataSet ;
        qb:structure <http://data.janburianek.com/ontology/dsd/mfcr-paid-invoices-2010-2014> ;
        obeu-dimension:organization <http://dbpedia.org/resource/Ministry_of_Finance_(Czech_Republic)> ;
        obeu-attribute:currency obeu-currency:CZK ;
        dcterms:source <https://raw.githubusercontent.com/jan-burianek/mfcr-spending-data/master/data-model/source/spending-mfcr-data-2010-2014.csv> .
} FROM <file:spending-mfcr-data.csv> WHERE {} LIMIT 1

CONSTRUCT {
    ?obs a qb:Observation ;
        obeu-dimension:partner ?partner ;
        obeu-dimension:operation-character ?op_char ;
        mfcr-attribute:payment-purpose ?purpose;
        mfcr-attribute:invoice-number ?invoice_number ;
        mfcr-dimension:transaction-date ?transaction_date ;
        mfcr-attribute:variable-symbol ?Variabilni_symbol ;
        obeu-measure:amount ?amount;
        qb:dataSet <http://data.janburianek.com/resource/dataset/mfcr-paid-invoices-2010-2014> .
}

FROM <file:spending-mfcr-data.csv>
WHERE {
    BIND(URI(CONCAT('http://data.janburianek.com/resource/dataset/mfcr-paid-invoices-2010-2014/observation/', ?Cislo_faktury, ?Variabilni_symbol, ?Cislo_radku)) AS ?obs)
    BIND(URI(CONCAT('http://data.janburianek.com/resource/organization/', ?Kod_partnera)) AS ?partner)
    BIND(
    	IF( ?Typ_dokladu = "D",
    		URI(CONCAT("http://data.openbudgets.eu/resource/codelist/operation-character/", "revenue")),	# else
    		URI(CONCAT("http://data.openbudgets.eu/resource/codelist/operation-character/", "expenditure")))
    	AS ?op_char)
    BIND(STRLANG(?Ucel_platby, "cs") AS ?purpose)
    BIND(xsd:string(?Cislo_faktury_dodavatele) AS ?invoice_number)
    BIND(xsd:date(?Datum_uhrady) AS ?transaction_date)
    BIND(ABS(xsd:decimal(?Castka___radek)) AS ?amount)
}

CONSTRUCT {
    ?partner a rov:RegisteredOrganization ;
        schema:legalName ?partner_legal_name .
}

FROM <file:spending-mfcr-data.csv>
WHERE {
    BIND(URI(CONCAT('http://data.janburianek.com/resource/organization/', ?Kod_partnera)) AS ?partner)
    BIND(xsd:string(?Dodavatel) AS ?partner_legal_name)
}